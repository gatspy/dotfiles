#!/bin/zsh
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS=${HOME}/Projects
export USERSHARE=/Users/Shared

# lazy load nvm by zsh-nvm
export NVM_LAZY_LOAD=true
export NVM_NO_USE=true
# export NVM_AUTO_USE=true

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# resolved command not found: compdef
# 1. This will perform chmod g-w for each file returned by compaudit to remove write access for group
# compaudit | xargs -I % chmod g-w "%"
# 2. This will perform chown to current user (Windows and Linux) for each file returned by compaudit
# compaudit | xargs -I % chown $USER "%"
# Remove all dump files (which normally speed up initialization)
# 3. rm ~/.zcompdump*
# 4. Regenerate completions file
# compinit

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load antibody plugins, must be placed after of compinit
source "${HOME}/.zsh_plugins.sh"

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at


# use .zsh.localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
if [[ -a ~/.zsh.localrc ]]
then
  source ~/.zsh.localrc

# FZF Setup {{{
# -------------
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
# }}}

# FZF ZLE extra bindkeys {{{

bindkey '^E' fzf-cd-widget

# }}}
fi


